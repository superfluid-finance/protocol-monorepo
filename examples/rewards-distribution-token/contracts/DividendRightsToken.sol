// SPDX-License-Identifier: MIT
pragma solidity 0.8.13;

import { SuperAppBase } from "@superfluid-finance/ethereum-contracts/contracts/apps/SuperAppBase.sol";

import { 
    ISuperfluid, 
    ISuperToken, 
    ISuperApp, 
    ISuperAgreement, 
    SuperAppDefinitions 
} from "@superfluid-finance/ethereum-contracts/contracts/interfaces/superfluid/ISuperfluid.sol"; 

import { IInstantDistributionAgreementV1 } from "@superfluid-finance/ethereum-contracts/contracts/interfaces/agreements/IInstantDistributionAgreementV1.sol";

import {IDAv1Library} from "@superfluid-finance/ethereum-contracts/contracts/apps/IDAv1Library.sol";

import { Ownable } from "@openzeppelin/contracts/access/Ownable.sol";
import { ERC20 } from "@openzeppelin/contracts/token/ERC20/ERC20.sol";

/**
 * The dividends rights token show cases two use cases
 * 1. Use Instant distribution agreement to distribute tokens to token holders.
 * 2. Use SuperApp framework to update `isSubscribing` when new subscription is approved by token holder.
 */
contract DividendRightsToken is
    Ownable,
    ERC20,
    SuperAppBase
{

    using IDAv1Library for IDAv1Library.InitData;

    IDAv1Library.InitData public idaV1;

    uint32 public constant INDEX_ID = 0;
    uint8 private _decimals;

    bytes32 internal constant IDAV1_ID = keccak256(
        "org.superfluid-finance.agreements.InstantDistributionAgreement.v1"
    );

    ISuperToken private _cashToken;

    // use callbacks to track approved subscriptions
    mapping (address => bool) public isSubscribing;

    constructor(
        string memory name,
        string memory symbol,
        ISuperToken cashToken,
        ISuperfluid _host
    )
        ERC20(name, symbol)
    {
        _cashToken = cashToken;

        uint256 configWord =
            SuperAppDefinitions.APP_LEVEL_FINAL |
            SuperAppDefinitions.BEFORE_AGREEMENT_TERMINATED_NOOP |
            SuperAppDefinitions.AFTER_AGREEMENT_TERMINATED_NOOP;

        _host.registerApp(configWord);

        idaV1 = IDAv1Library.InitData(
            _host,
            IInstantDistributionAgreementV1(
                address(_host.getAgreementClass(IDAV1_ID))
            )
        );

        idaV1.createIndex(_cashToken, INDEX_ID);

        transferOwnership(msg.sender);
        _decimals = 0;
    }


    function decimals() public view override returns (uint8) {
        return _decimals;
    }

    function _checkSubscription(
        ISuperToken superToken,
        bytes calldata ctx,
        bytes32 agreementId
    )
        private
    {
        ISuperfluid.Context memory context = idaV1.host.decodeCtx(ctx);
        // only interested in the subscription approval callbacks
        if (context.agreementSelector == IInstantDistributionAgreementV1.approveSubscription.selector) {
            address publisher;
            uint32 indexId;
            bool approved;
            uint128 units;
            uint256 pendingDistribution;
            (publisher, indexId, approved, units, pendingDistribution) =
                idaV1.ida.getSubscriptionByID(superToken, agreementId);

            // sanity checks for testing purpose
            require(publisher == address(this), "DRT: publisher mismatch");
            require(indexId == INDEX_ID, "DRT: publisher mismatch");

            if (approved) {
                isSubscribing[context.msgSender /* subscriber */] = true;
            }
        }
    }

    /// @dev Issue new `amount` of giths to `beneficiary`
    function issue(address beneficiary, uint256 amount) external onlyOwner {
        // then adjust beneficiary subscription units
        uint256 currentAmount = balanceOf(beneficiary);

        // first try to do ERC20 mint
        ERC20._mint(beneficiary, amount);

        idaV1.updateSubscriptionUnits(_cashToken, INDEX_ID, beneficiary, uint128(currentAmount) + uint128(amount));

    }

    /// @dev Distribute `amount` of cash among all token holders
    function distribute(uint256 cashAmount) external onlyOwner {
        (uint256 actualCashAmount,) = idaV1.ida.calculateDistribution(
            _cashToken,
            address(this), 
            INDEX_ID,
            cashAmount);

        _cashToken.transferFrom(owner(), address(this), actualCashAmount);

        idaV1.distribute(_cashToken, INDEX_ID, actualCashAmount);
    }

    /// @dev ERC20._transfer override
    function _transfer(address sender, address recipient, uint256 amount) internal override {
        uint128 senderUnits = uint128(ERC20.balanceOf(sender));
        uint128 recipientUnits = uint128(ERC20.balanceOf(recipient));
        // first try to do ERC20 transfer
        ERC20._transfer(sender, recipient, amount);

        idaV1.updateSubscriptionUnits(_cashToken, INDEX_ID, sender, senderUnits - uint128(amount));

        idaV1.updateSubscriptionUnits(_cashToken, INDEX_ID, recipient, recipientUnits + uint128(amount));

    }

    function beforeAgreementCreated(
        ISuperToken superToken,
        address agreementClass,
        bytes32 /* agreementId */,
        bytes calldata /*agreementData*/,
        bytes calldata /*ctx*/
    )
        external view override
        onlyHost
        onlyIDA(agreementClass)
        returns (bytes memory data)
        
    {
        require(superToken == _cashToken, "DRT: Unsupported cash token");
        return new bytes(0);
    }

    function afterAgreementCreated(
        ISuperToken superToken,
        address agreementClass,
        bytes32 agreementId,
        bytes calldata /*agreementData*/,
        bytes calldata /*cbdata*/,
        bytes calldata ctx
    )
        external override
        onlyHost
        onlyIDA(agreementClass)
        returns(bytes memory newCtx)
    {
        _checkSubscription(superToken, ctx, agreementId);
        newCtx = ctx;
    }

    function beforeAgreementUpdated(
        ISuperToken superToken,
        address agreementClass,
        bytes32 /* agreementId */,
        bytes calldata /*agreementData*/,
        bytes calldata /*ctx*/
    )
        external view override
        onlyHost
        onlyIDA(agreementClass)
        returns (bytes memory data)
    {
        require(superToken == _cashToken, "DRT: Unsupported cash token");
        return new bytes(0);
    }

    function afterAgreementUpdated(
        ISuperToken superToken,
        address agreementClass,
        bytes32 agreementId,
        bytes calldata /*agreementData*/,
        bytes calldata /*cbdata*/,
        bytes calldata ctx
    )
        external override
        onlyHost
        onlyIDA(agreementClass)
        returns(bytes memory newCtx)
    {
        _checkSubscription(superToken, ctx, agreementId);
        newCtx = ctx;
    }

    function _isIDAv1(address agreementClass) private view returns (bool) {
        return ISuperAgreement(agreementClass).agreementType() == IDAV1_ID;
    }

    modifier onlyHost() {
        require(
            msg.sender == address(idaV1.host),
            "Only host can call callback"
        );
        _;
    }

    modifier onlyIDA(address agreementClass) {
        require(_isIDAv1(agreementClass), "Only IDAv1 supported");
        _;
    }

}
